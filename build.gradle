plugins {
    id "java-gradle-plugin"
    id "groovy"
}

repositories {
    mavenCentral()
}

sourceSets {
    functionalTest {
        groovy {
            srcDir file('src/functionalTest/groovy')
        }
        resources {
            srcDir file('src/functionalTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check.dependsOn functionalTest

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

dependencies {
    compile 'org.zalando.stups:swagger-codegen-common:+'
    compile 'org.zalando.stups:swagger-codegen-template-jaxrs-interfaces:+'
    compile 'org.zalando.stups:swagger-codegen-template-spring-interfaces:+'

    testCompile 'org.assertj:assertj-core:3.6.2'
    testCompile 'junit:junit:4.12'

    functionalTestCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}
